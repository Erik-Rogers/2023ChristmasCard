10 SCREEN 9   ' Set graphics mode (change this depending on your system capabilities)
15 COLOR 7,1
20 CLS
21 LOCATE 2,20: PRINT "Merry Christmas from Information Systems"
30 ' Tree parameters
40 TREEHEIGHT = 150
50 TREEWIDTH = 120
60 TREECENTERX = 320
70 TREECENTERY = 50
80 TRUNKHEIGHT = 20
90 TRUNKWIDTH = 20
100 GOSUB 300 'DrawTree(TREEHEIGHT, TREEWIDTH, TREECENTERX, TREECENTERY, TRUNKHEIGHT, TRUNKWIDTH)
110 GOSUB 400 'DisplayMenu()
120 GOSUB 500 'ProcessMenuInput()
130 SCREEN 0
140 CLS
150 END
160 'End of Main Program
299 'SUBROUTINES
300 'SUB DrawTree(TREEHEIGHT, TREEWIDTH, TREECENTERX, TREECENTERY, TRUNKHEIGHT, TRUNKWIDTH)
305 COLOR 2,1 ' Set color for the tree
310 ' Calculate the points for the tree
315 X1 = TREECENTERX - TREEWIDTH / 2
320 X2 = TREECENTERX + TREEWIDTH / 2
325 YBottom = TREECENTERY + TREEHEIGHT
330 ' Draw the triangle for the tree
335 LINE (X1, YBottom)-(TREECENTERX, TREECENTERY) ' Left side of the tree
340 LINE -(X2, YBottom) ' Right side of the tree
345 LINE -(X1, YBottom) ' Base of the tree
350 ' Use PAINT to fill the tree
355 PAINT (TREECENTERX, TREECENTERY + TREEHEIGHT / 2), 2
360 ' Draw the trunk in brown
365 COLOR 4,1
370 LINE (TREECENTERX - TRUNKWIDTH / 2, YBottom)-(TREECENTERX + TRUNKWIDTH / 2, YBottom + TRUNKHEIGHT), 4, BF
398 RETURN
399 'END SUB
400 'SUB DisplayMenu()
405 COLOR 7,1
410 LOCATE 20, 1: PRINT "1. SILENT NIGHT"
420 PRINT "2. JINGLE BELLS"
430 PRINT "3. FELIZ NAVIDAD"
440 LOCATE 20,40: PRINT "4. WALKING IN A WINTER WONDERLAND"
460 LOCATE 21,40: PRINT "9. EXIT"
498 RETURN
499 'END SUB
500 'SUB ProcessMenuInput()
510 SEL$ = INKEY$
520 IF SEL$ = "1" THEN GOSUB 600: GOTO 510 'SilentNight()
521 IF SEL$ = "2" THEN GOSUB 700: GOTO 510 'JingleBells()
522 IF SEL$ = "3" THEN GOSUB 800: GOTO 510 'FelizNavidad()
523 IF SEL$ = "4" THEN GOSUB 900: GOTO 510 'WinterWonderland()
540 IF SEL$ = "9" THEN SCREEN  0: CLS: END 'End the program
550 GOTO 500
560 RETURN
599 'END SUB
600 'SUB SilentNight()
610 PLAY "O3T60L8F.G16FD4.F.G16FD4L4>CC8<A.B-B-8F."
620 PLAY "GG8B-8.A16G8F8.G16F8D.GG8B-8.A16G8F8.G16F8D."
630 PLAY ">CC8E-8.C16<A8B-.>D.<L8B-FDF.E-16C<B-2."
698 RETURN
699 'END SUB
700 'SUB JingleBells()
710 PLAY "O3T240L4 DBAG D2.D8D8 DBAG E1"
720 PLAY "E>C<BA F#1 >DDC<A B1"
730 PLAY "DBAG D1 DBAG E2.E"
740 PLAY "E>C<BA >DDDD EDC<A G2P2"
750 PLAY "BBB2 BBB2 B>D<G.A8B1"
760 PLAY ">CCC.C8 C<BBB8B8 BAABA2>D2"
770 PLAY "<BBB2 BBB2 B>D<G.A8 B1"
780 PLAY ">CCC.C C<BBB8B8 >DDC<A G1"
798 RETURN
799 'END SUB
800 'SUB FelizNavidad()
810 PLAY "O3T120L4 P4.G8>C<B8>C8< A2O1AAB8P4"
820 PLAY "O3A8>DC8<A8 G2O1GGF8FP4 O3G8>C<B8>C8<AP8F8F8A8A8G8 G8G8G8G8FF8E8 E2P2"
830 PLAY "P4.G8>C<B8>C8< A2O1AAB8P4"
840 PLAY "O3A8>DC8<A8 G2O1GGF8FP4 O3G8>C<B8>C8<AP8F8F8A8A8G8 G8G8G8G8FF8E8 E2P2"
850 PLAY "O4P8E8E8E8ED8C8 C<A8A8A8A."
860 PLAY "O1B8O4D8D8D8DC8<A8 AG8G8G8G."
870 PLAY "O4O1E8O4E8E8E8ED8C8 C<A8A8A8A8>D8C8< B8B.>C8D8D8D8 DC2O1E8E8"
875 PLAY "O4P8E8E8E8ED8C8 C<A8A8A8A."
880 PLAY "O1B8O4D8D8D8DC8<A8 AG8G8G8G."
885 PLAY "O4O1E8O4E8E8E8ED8C8 C<A8A8A8A8>D8C8< B8B.>C8D8D8D8 DC2O1E8E8"
898 RETURN
899 'END SUB
900 'SUB WinterWonderland()
910 PLAY "O2T120L8 AA A2.AAF#A2.AAA2.AAF#A2.P8"
920 PLAY "A>C#C#C#<B2. AAAG2. F#F#F#F#EEEED4P4P4"
930 'PLAY "AA A2.AAF#A2.AAA2.AAF#A2.P8A >C#C#C#<B2. AAAG2. F#F#F#F#EEEED4P4P4P4"
940 PLAY "C#C#BBAA>CC"
950 PLAY "<B4G2. C#C#BBAA>CC<B1 EE>C#C#<BB>DD C#<A2.A>C#C#<AABBG#4A2."
960 PLAY "AA A2.AAF#A2.AAA2.AAF#A2.P8"
970 PLAY "A>C#C#C#<B2. AAAG2. F#F#F#F#EEEED4P4P4"
998 RETURN
999 'END SUB